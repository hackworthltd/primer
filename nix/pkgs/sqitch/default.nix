# Based on upstream nixpkgs package:
#
# https://github.com/NixOS/nixpkgs/blob/3557bb49b065495de31b0cbe71de8f2f380c2706/pkgs/development/tools/misc/sqitch/default.nix
#
# But we override the Perl module, which is not well-maintained upstream.

{ stdenv
, lib
, perlPackages
, fetchurl
, makeWrapper
, shortenPerlShebang
, mysqlSupport ? false
, postgresqlSupport ? false
}:

let

  # Generated by running these commands:
  #
  # nix-generate-from-cpan Algorithm::Backoff
  # nix-generate-from-cpan App::Sqitch

  AlgorithmBackoff = perlPackages.buildPerlPackage {
    pname = "Algorithm-Backoff";
    version = "0.009";
    src = fetchurl {
      url = "mirror://cpan/authors/id/P/PE/PERLANCAR/Algorithm-Backoff-0.009.tar.gz";
      sha256 = "9f0ffcdf1e65a88022d6412f46ad977ede5a7b64be663009d13948fe8c9d180b";
    };
    buildInputs = with perlPackages; [ TestException TestNumberDelta ];
    meta = {
      homepage = "https://metacpan.org/release/Algorithm-Backoff";
      description = "Various backoff strategies for retry";
      license = with lib.licenses; [ artistic1 gpl1Plus ];
    };
  };

  sqitch = perlPackages.buildPerlModule {
    pname = "App-Sqitch";
    version = "1.2.1";
    src = fetchurl {
      url = "mirror://cpan/authors/id/D/DW/DWHEELER/App-Sqitch-v1.2.1.tar.gz";
      sha256 = "020835a13429effd8fda12d5627604ecf99293775918f4f8ba9ccc5ed796e5e7";
    };
    buildInputs = with perlPackages; [
      CaptureTiny
      ModuleRuntime
      TestDeep
      TestDir
      TestException
      TestFile
      TestFileContents
      TestMockModule
      TestMockObject
      TestNoWarnings
      TestWarn
    ];
    propagatedBuildInputs =
      [ AlgorithmBackoff ]
      ++ (with perlPackages; [
        Clone
        ConfigGitLike
        DBI
        DateTime
        DateTimeTimeZone
        DevelStackTrace
        EncodeLocale
        HashMerge
        IOPager
        IPCRun3
        IPCSystemSimple
        ListMoreUtils
        Moo
        PathClass
        PerlIOutf8_strict
        StringFormatter
        StringShellQuote
        SubExporter
        TemplateTiny
        Throwable
        TryTiny
        TypeTiny
        URI
        URIdb
        libintlperl
        namespaceautoclean
      ]);

    # Can't find its home directory during Nix builds.
    doCheck = false;

    meta = {
      homepage = "https://sqitch.org/";
      description = "Sensible database change management";
      license = lib.licenses.mit;
    };
  };

  modules = with perlPackages; [ ]
    ++ lib.optional mysqlSupport DBDmysql
    ++ lib.optional postgresqlSupport DBDPg;
in

stdenv.mkDerivation {
  pname = "sqitch";
  version = sqitch.version;

  nativeBuildInputs = [ makeWrapper ] ++ lib.optional stdenv.isDarwin shortenPerlShebang;

  src = sqitch;
  dontBuild = true;

  installPhase = ''
    mkdir -p $out/bin
    for d in bin/sqitch etc lib share ; do
      # make sure dest alreay exists before symlink
      # this prevents installing a broken link into the path
      if [ -e ${sqitch}/$d ]; then
        ln -s ${sqitch}/$d $out/$d
      fi
    done
  '' + lib.optionalString stdenv.isDarwin ''
    shortenPerlShebang $out/bin/sqitch
  '';
  dontStrip = true;
  postFixup = ''
    wrapProgram $out/bin/sqitch --prefix PERL5LIB : ${perlPackages.makeFullPerlPath modules}
  '';

  meta = {
    inherit (sqitch.meta) description homepage license platforms;
  };
}
